resource "aws_instance" "master" {
  depends_on = [ null_resource.prepare_bastion ]

  ami                         = var.aws_ami
  instance_type               = var.ec2_instance_class  
  associate_public_ip_address = false
  ipv6_address_count          = var.enable_ipv6 ? 1 : 0
  count                       = var.no_of_server_nodes
  root_block_device {
    volume_size          = var.volume_size
    volume_type          = "gp3"
    iops                  = 3000
    throughput            = 125
    delete_on_termination = true
  }
  subnet_id              = var.subnets
  availability_zone      = var.availability_zone
  vpc_security_group_ids = [var.sg_id]
  key_name               = var.key_name
  tags = {
    Name                 = "${var.resource_name}-${local.resource_tag}-server${count.index + 1}"
    Team                 = local.resource_tag
  } 

  provisioner "local-exec" { 
    command = "aws ec2 wait instance-status-ok --region ${var.region} --instance-ids ${self.id}" 
  }
}

resource "aws_instance" "worker" {
  depends_on = [ aws_instance.master ]

  ami                         = var.aws_ami
  instance_type               = var.ec2_instance_class  
  associate_public_ip_address = false
  ipv6_address_count          = var.enable_ipv6 ? 1 : 0
  count                       = var.no_of_worker_nodes
  root_block_device {
    volume_size          = var.volume_size
    volume_type          = "gp3"
    iops                  = 3000
    throughput            = 125
    delete_on_termination = true
  }
  subnet_id              = var.subnets
  availability_zone      = var.availability_zone
  vpc_security_group_ids = [var.sg_id]
  key_name               = var.key_name
  tags = {
    Name                 = "${var.resource_name}-${local.resource_tag}-worker${count.index + 1}"
    Team                 = local.resource_tag
  }

  provisioner "local-exec" { 
    command = "aws ec2 wait instance-status-ok --region ${var.region} --instance-ids ${self.id}" 
  }
}

resource "aws_instance" "windows_worker" {
  depends_on = [ aws_instance.master ]

  ami                         = var.windows_aws_ami
  instance_type               = var.windows_ec2_instance_class  
  associate_public_ip_address = false
  ipv6_address_count          = var.enable_ipv6 ? 1 : 0
  count                       = var.no_of_windows_worker_nodes
  
  root_block_device {
    volume_size          = 50
    volume_type          = "gp3"
    iops                  = 3000
    throughput            = 125
    delete_on_termination = true
  }
  subnet_id              = var.subnets
  availability_zone      = var.availability_zone
  vpc_security_group_ids = [var.sg_id]
  key_name               = var.key_name
  get_password_data      = true
  user_data              = <<-EOF
    <powershell>
    $ErrorActionPreference = "Stop"

    Add-WindowsCapability -Online -Name OpenSSH.Server~~~~0.0.1.0
    Set-Service -Name sshd -StartupType Automatic
    New-NetFirewallRule -Name sshd -DisplayName 'OpenSSH SSH Server' -Enabled True -Direction Inbound -Protocol TCP -Action Allow -LocalPort 22
    Start-Service sshd

    New-ItemProperty -Path "HKLM:\SOFTWARE\OpenSSH" -Name DefaultShell -Value "C:\Windows\System32\WindowsPowerShell\v1.0\powershell.exe" -PropertyType String -Force
    
    # 1. Capture the multi-line tfvars PEM string from Terraform
    $rawPemKey = @"
    ${trimspace(file(var.access_key))}
    "@

    # 2. Setup standard paths for Windows Administrator SSH keys
    $tempPemPath  = "$env:TEMP\temp_key.pem"
    $adminKeyPath = "$env:ProgramData\ssh\administrators_authorized_keys"

    # 3. Create the target SSH directory if it doesn't exist
    $sshConfigDir = Split-Path $adminKeyPath
    if (-not (Test-Path $sshConfigDir)) { New-Item -ItemType Directory -Path $sshConfigDir | Out-Null }

    # 4. Safely drop the PEM contents onto disk temporarily
    Set-Content -Path $tempPemPath -Value $rawPemKey -Encoding ascii

    # 5. Extract the OpenSSH public key format and append it to the authorized file
    ssh-keygen -y -f $tempPemPath | ForEach-Object { Add-Content -Path $adminKeyPath -Value $_ -Encoding ascii }
    icacls.exe $authorizedKeys /inheritance:r /grant "Administrators:F" /grant "SYSTEM:F"
    Restart-Service sshd

    Enable-WindowsOptionalFeature -Online -FeatureName Containers -All -NoRestart
    Restart-Computer -Force
    </powershell>
  EOF
  tags = {
    Name                 = "${var.resource_name}-${local.resource_tag}-windows-worker${count.index + 1}"
    Team                 = local.resource_tag
  }

  provisioner "local-exec" { 
    command = "aws ec2 wait instance-status-ok --region ${var.region} --instance-ids ${self.id}" 
  }
}

resource "aws_instance" "bastion" {
  ami                         = var.aws_ami
  instance_type               = var.ec2_instance_class  
  associate_public_ip_address = true
  ipv6_address_count          = var.enable_ipv6 ? 1 : 0
  count                       = var.no_of_bastion_nodes == 0 ? 0 : 1
  
  connection {
    type          = "ssh"
    user          = var.aws_user
    host          = self.public_ip
    private_key   = file(var.access_key)
  }
  root_block_device {
    volume_size          = var.volume_size
    volume_type          = "gp3"
    iops                  = 3000
    throughput            = 125
    delete_on_termination = true
  }
  subnet_id              = var.bastion_subnets
  availability_zone      = var.availability_zone
  vpc_security_group_ids = [var.sg_id]
  key_name               = var.key_name
  tags = {
    Name                 = "${var.resource_name}-${local.resource_tag}-bastion"
    Team                 = local.resource_tag
  }
  
  provisioner "file" {
    source = "../../config/.ssh/aws_key.pem"
    destination = "/tmp/${var.key_name}.pem"
  }

  provisioner "file" {
    source = "setup/get_artifacts.sh"
    destination = "/tmp/get_artifacts.sh"
  }

  provisioner "file" {
    source = "setup/install_product.sh"
    destination = "/tmp/install_product.sh"
  }

  provisioner "file" {
    source = "setup/bastion_prepare.sh"
    destination = "/tmp/bastion_prepare.sh"
  }

  provisioner "file" {
    source = "setup/podman_cmds.sh"
    destination = "/tmp/podman_cmds.sh"
  }
  provisioner "file" {
    source = "setup/private_registry.sh"
    destination = "/tmp/private_registry.sh"
  }

  provisioner "file" {
    source = "setup/system_default_registry.sh"
    destination = "/tmp/system_default_registry.sh"
  }

  provisioner "file" {
    source = "setup/windows_install.ps1"
    destination = "/tmp/windows_install.ps1"
  }
  provisioner "file" {
    source = "setup/basic-registry"
    destination = "/tmp"
  }
  provisioner "local-exec" { 
    command = "aws ec2 wait instance-status-ok --region ${var.region} --instance-ids ${self.id}" 
  }
}

resource "null_resource" "prepare_bastion" {
  depends_on = [ aws_instance.bastion ]
  connection {
    type          = "ssh"
    user          = var.aws_user
    host          = aws_instance.bastion[0].public_ip
    private_key   = file(var.access_key)
  }

  provisioner "remote-exec" {
    inline = [<<-EOT
      sudo cp /tmp/${var.key_name}.pem /tmp/*.sh /tmp/*.ps1 ~/
      sudo cp -r /tmp/basic-registry ~/
      sudo chmod +x bastion_prepare.sh
      sudo ./bastion_prepare.sh
    EOT
    ]
  }
}

locals {
  resource_tag =  "distros-qa"
}
